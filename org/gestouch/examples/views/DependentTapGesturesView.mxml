<?xml version="1.0" encoding="utf-8"?>
<views:ExampleViewBase xmlns:fx="http://ns.adobe.com/mxml/2009"
    xmlns:s="library://ns.adobe.com/flex/spark"
	xmlns:mx="library://ns.adobe.com/flex/mx"
	xmlns:views="org.gestouch.examples.views.*"
	xmlns:controls="org.gestouch.examples.controls.*"
	xmlns:containers="org.gestouch.examples.containers.*"
	>
	
	<fx:Script>
		<![CDATA[
			import com.greensock.TweenMax;

			import org.gestouch.events.GestureEvent;
			import org.gestouch.gestures.TapGesture;
			
			
			private var singleTapGesture:TapGesture;
			private var doubleTapGesture:TapGesture;
			
			
			override protected function onViewActivate():void
			{
				super.onViewActivate();
				
				singleTapGesture = new TapGesture(button);
				singleTapGesture.addEventListener(org.gestouch.events.GestureEvent.GESTURE_RECOGNIZED, onGesture);
				
				doubleTapGesture = new TapGesture(button);
				doubleTapGesture.addEventListener(org.gestouch.events.GestureEvent.GESTURE_RECOGNIZED, onGesture);
				// probably you want to set a bit smaller value for maxTapDelay
				// so double tap will fail a bit more quickly. default is 400ms
				doubleTapGesture.maxTapDelay = 300;
				doubleTapGesture.numTapsRequired = 2;
				
				singleTapGesture.requireGestureToFail(doubleTapGesture);
			}
			
			
			override protected function onViewDeactivate():void
			{
				super.onViewDeactivate();
				
				if (singleTapGesture)
				{
					singleTapGesture.dispose();
					singleTapGesture = null;
				}
				if (doubleTapGesture)
				{
					doubleTapGesture.dispose();
					doubleTapGesture = null;
				}
			}
			

			private function onGesture(event:org.gestouch.events.GestureEvent):void
			{
				var tapGesture:TapGesture = event.target as TapGesture;
				if (tapGesture == singleTapGesture)
				{
					label.text = "Signle-tap recognized.";
				}
				else
				{
					label.text = "Double-tap recognized.";
				}
				
				TweenMax.fromTo(label, 4,
					{autoAlpha: 1},
					{autoAlpha: 0}
				);
				
				TweenMax.fromTo(button, 1,
					{glowFilter: {color: 0xCCCCCC * Math.random(), blurX: 64, blurY: 64, strength: 3, alpha: 1, quality: 1}},
					{glowFilter: {alpha: 0, remove: true}}
				);
			}
		]]>
	</fx:Script>
	
	<s:Label id="label" />
	<s:Button id="button" label="Tap or Double-Tap Me" minWidth="250" minHeight="200" horizontalCenter="0" verticalCenter="0"/>
	
</views:ExampleViewBase>